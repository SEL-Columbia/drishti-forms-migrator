['java', 'idea', 'application', 'fatjar'].each({ apply plugin: it })

sourceCompatibility = 1.5
version = '1.0'

project.ext {
    dropwizardVersion = '0.6.2'
}

mainClassName = 'app.ApplicationService'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "http://repository.codehaus.org/org/codehaus"
        }
    }

    dependencies {
        classpath group: 'eu.appsatori', name: 'gradle-fatjar-plugin', version: '0.1.3'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://repository.codehaus.org/org/codehaus"
    }
}

dependencies {
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-jdbi', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-migrations', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-hibernate', version: dropwizardVersion
    compile group: 'org.codehaus.jackson', name: 'jackson-core-lgpl', version: '1.7.1'
    compile fileTree('lib')
    runtime group: 'postgresql', name: 'postgresql', version: '9.1-901.jdbc4'
}

run {
    args 'server', 'content-migrator.yml'
}

sourceSets {
    test {
        java.srcDir file('src/test/')
    }
}

fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

task runMigrations(type: Exec) {
    commandLine 'java',  '-jar', 'build/libs/drishti-content-migrator-1.0-fat.jar', 'db', 'migrate', 'content-migrator.yml'
}

task cleanDb(type: Exec) {
    commandLine 'java',  '-jar', 'build/libs/drishti-content-migrator-1.0-fat.jar', 'db', 'drop-all', '--confirm-delete-everything', 'content-migrator.yml'
}

task migrations(dependsOn: ['fatJar', 'runMigrations'])