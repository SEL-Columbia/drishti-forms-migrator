['java', 'idea', 'application', 'maven'].each({ apply plugin: it })

project.ext {
    dropwizardVersion = '0.6.2'
    configFile = 'content-migrator.yml'
}

ext.packaging = 'tar'

mainClassName = 'app.ApplicationService'
version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url "http://repository.codehaus.org/org/codehaus"
    }
}

artifacts {
    archives distTar
}

dependencies {
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'com.sun.jersey', name: 'jersey-client', version: '1.17.1'
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-jdbi', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-migrations', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-hibernate', version: dropwizardVersion
    compile group: 'org.codehaus.jackson', name: 'jackson-core-lgpl', version: '1.9.10'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.14.2'
    compile fileTree('lib')
    runtime group: 'postgresql', name: 'postgresql', version: '9.1-901.jdbc4'

    testCompile 'junit:junit:4.8.2'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

run {
    args 'server', configFile
}

if (shouldDoFatJar()) {
    jar {
        manifest {
            attributes "Main-Class": mainClassName
        }

        doFirst {
            from(configurations.runtime.resolve().collect { it.isDirectory() ? it : zipTree(it) }) {
                exclude 'META-INF/*.SF'
                exclude 'META-INF/*.DSA'
                exclude 'META-INF/*.RSA'
            }
        }
    }
}

task migrations(dependsOn: 'jar') << {
    javaexec {
        main = '-jar'
        args = [jar.archivePath, 'db', 'migrate', configFile]
    }
}

task cleanDb(dependsOn: 'jar') << {
    javaexec {
        main = '-jar'
        args = [jar.archivePath, 'db', 'drop-all', '--confirm-delete-everything', configFile]
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://nexus.motechproject.org/content/repositories/drishti-forms-migrator") {
                authentication(userName: System.getenv('NEXUS_USERNAME'), password: System.getenv('NEXUS_PASSWORD'))
            }
            pom.artifactId = "drishti-forms-migrator"
            pom.groupId = "app"
        }
    }
}

def shouldDoFatJar() {
    return project.hasProperty("mode") && "dev".equals(project.property("mode"))
}