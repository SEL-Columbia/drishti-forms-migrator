['java', 'idea', 'application', 'maven', 'fatjar'].each({ apply plugin: it })

project.ext {
    dropwizardVersion = '0.7.1'
    configFile = 'content-migrator.yml'
}

ext.packaging = 'tar'

mainClassName = 'app.ApplicationService'
version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'eu.appsatori', name: 'gradle-fatjar-plugin', version: '0.1.3'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://repository.codehaus.org/org/codehaus"
    }
}

artifacts {
    archives distTar
}

dependencies {
    compile group: 'com.sun.jersey', name: 'jersey-client', version: '1.17.1'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-migrations', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-hibernate', version: dropwizardVersion
    compile group: 'org.codehaus.jackson', name: 'jackson-core-lgpl', version: '1.9.10'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.14.2'
    compile fileTree('lib')
    runtime group: 'postgresql', name: 'postgresql', version: '9.1-901.jdbc4'

    testCompile 'junit:junit:4.8.2'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

run {
    args 'server', configFile
}

fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

task migrations(dependsOn : "fatJar") << {
    javaexec {
        main = '-jar'
        args = [fatJar.archivePath, 'db', 'migrate', configFile]
    }
}

task cleanDb(dependsOn : "fatJar") << {
    javaexec {
        main = '-jar'
        args = [fatJar.archivePath, 'db', 'drop-all', '--confirm-delete-everything', configFile]
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://nexus.motechproject.org/content/repositories/drishti-forms-migrator") {
                authentication(userName: System.getenv('NEXUS_USERNAME'), password: System.getenv('NEXUS_PASSWORD'))
            }
            pom.artifactId = "drishti-forms-migrator"
            pom.groupId = "app"
        }
    }
}

task foo << {
    println fatJar.archivePath
}